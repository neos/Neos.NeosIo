prototype(Neos.NeosIo:PostRenderer) < prototype(Neos.Fusion:Template) {
    templatePath = 'resource://Neos.NeosIo/Private/Templates/NodeTypes/Post.html'

    node = ${node}
    datePublished = ${q(node).property('datePublished')}
    title = ${q(node).property('title')}
    image = ${q(node).property('image')}
    summary = ${q(node).property('summary')}
    authors = ${q(node).property('authors')}
    authorAlternative = ${q(node).property('authorAlternative')}
    relatedDocuments = ${q(node).property('relatedDocuments')}
    author = ${this.authors ? Data.users(this.authors) + this.authorAlternative : this.authorAlternative}

    content = Neos.Neos:ContentCollection {
        nodePath = 'main'
    }

    backToListButton = Neos.NeosIo:Button {
        style = 'btn--solidPrimary'
        buttonLabel = 'Back to list'
        alignment = 'Right'
        link = Neos.Neos:NodeUri {
            node = ${q(node).parents('[instanceof Neos.NodeTypes:Page]').get(0)}
            absolute = true
        }
        @process.contentElementWrapping >
    }

    latest = ${q(site).find('[instanceof Neos.NeosIo:Post]').slice(0, 6).sort('datePublished', 'DESC').get()}
}

prototype(Neos.NeosIo:Post.Short) < prototype(Neos.NeosIo:PostRenderer) {
    templatePath = 'resource://Neos.NeosIo/Private/Templates/FusionObjects/PostShort.html'
    content >
}
