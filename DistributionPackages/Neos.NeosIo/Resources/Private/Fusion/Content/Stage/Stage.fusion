prototype(Neos.NeosIo:Stage) < prototype(Neos.Neos:ContentComponent) {
    isContentTextInverted = ${q(node).property('isContentTextInverted')}
    backgroundColor = ${q(node).property('backgroundColor')}
    backgroundImage = ${q(node).property('backgroundImage')}
    backgroundShadePercentage = ${q(node).property('backgroundShadePercentage')}
    backgroundImageFixed = ${q(node).property('backgroundImageFixed')}
    alternativeText = ${q(node).property('alternativeText')}
    title = ${q(node).property('title')}
    isContentExtraPadded = ${q(node).property('isContentExtraPadded') || this.backgroundImage  != null}

    renderer = afx`
        <section class={['stage', props.backgroundColor ? 'stage--' + props.backgroundColor : 'stage--bright']}>
            <div class={['stage__contents', props.isContentTextInverted && 'u-invertText', props.isContentExtraPadded && 'stage__contents--extraPadding']}>
                <Neos.Neos:ContentCollection nodePath="contents" />
            </div>
            <Neos.NeosIo:Stage.FixedBackgroundImage
                @if.fixed={props.backgroundImageFixed}
                title={props.title}
                backgroundImage={props.backgroundImage}
                altText={props.alternativeText}
                backgroundShadePercentage={props.backgroundShadePercentage}
            />
            <Neos.NeosIo:Stage.BackgroundImage
                @if.notFixed={!props.backgroundImageFixed}
                title={props.title}
                backgroundImage={props.backgroundImage}
                altText={props.alternativeText}
                backgroundShadePercentage={props.backgroundShadePercentage}
            />
        </section>
    `
}
