prototype(Neos.NeosIo:DocumentationPageSubMenuItemRenderer) < prototype(Neos.Fusion:DataStructure) {
    node = ${item}
    label = ${q(item).property('title')}
    // the current page should scroll to the top
    anchorName = ${documentNode.aggregateId == item.aggregateId ? 'mainContent' : ''}
    linkAttributes = ''
    subItems = Neos.Fusion:Map {
        // The Fusion code below extracts all headlines and renders them as on-page subItems into the menu.
        // It is necessary to set an "anchorName" for each headline for these links to work.

        @if.onlyAppendOnPageSubItemsForCurrentDocumentNode = ${documentNode.aggregateId == item.aggregateId}
        items = ${q(documentNode).find('[instanceof Neos.NeosIo:SemanticSection]').children('contents').children('[instanceof Neos.NodeTypes:Headline]').filter('[title ^= "<h2>"][anchorName][anchorName != ""]')}
        itemRenderer = Neos.Fusion:DataStructure {
            @context.currentSection = ${q(item).parents('[instanceof Neos.NeosIo:SemanticSection]').get(0)}
            node = ${documentNode}
            label = ${String.stripTags(q(item).property('title'))}
            anchorName = ${q(item).property('anchorName')}
            linkAttributes = Neos.Fusion:DataStructure {
                data-component = 'ScrollTo'
                data-targetSelector = ${'a[name=' + q(item).property('anchorName') + ']'}
            }
            subItems = Neos.Fusion:Map {
                items = ${q(currentSection).children('contents').children('[instanceof Neos.NodeTypes:Headline]').filter('[title ^= "<h3>"][anchorName][anchorName != ""]')}
                itemRenderer = Neos.Fusion:DataStructure {
                    node = ${documentNode}
                    label = ${String.stripTags(q(item).property('title'))}
                    anchorName = ${q(item).property('anchorName')}
                    linkAttributes = Neos.Fusion:DataStructure {
                        data-component = 'ScrollTo'
                        data-targetSelector = ${'a[name=' + q(item).property('anchorName') + ']'}
                    }
                }
            }
        }
    }
}
