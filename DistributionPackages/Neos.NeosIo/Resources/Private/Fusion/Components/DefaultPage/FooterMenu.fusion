prototype(Neos.NeosIo:DefaultPage.FooterMenu) < prototype(Neos.Fusion:Component) {
    footerContainer = ${q(site).property('footerContentContainer')}
    items = ${q(this.footerContainer).children('[instanceof Neos.Neos:Document]').get()}

    @if.hasFooterContainer = ${this.footerContainer}

    renderer = afx`
      <ul class="nav nav--stacked siteFooter__nav">
          <Neos.Fusion:Loop items={props.items}>
              <li>
                  <Neos.Neos:NodeLink node={item} attributes.title={Neos.Node.label(item)} />
              </li>
          </Neos.Fusion:Loop>
      </ul>
    `

    @cache {
        mode = 'cached'

        entryIdentifier {
            site = ${Neos.Caching.entryIdentifierForNode(site)}
        }

        entryTags {
            footerContainer = ${Neos.Caching.nodeTag(q(site).property('footerContentContainer') || [])}
            childNodes = ${Neos.Caching.descendantOfTag(q(site).property('footerContentContainer') || [])}
        }
    }
}
