prototype(Neos.NeosIo:DefaultPage.Menu) < prototype(Neos.Fusion:Renderer) {
    renderPath = '/mainNavigation'
}

prototype(Neos.NeosIo:DefaultPage.Menu.Renderer) < prototype(Neos.Fusion:Component) {
    items = Neos.Neos:MenuItems {
        entryLevel = 1
        maximumLevels = 2
        filter = 'Neos.Neos:Document,!Neos.NeosIo:Reference.ShowCase'
        itemUriRenderer = ''
    }

    renderer = afx`
        <nav role="navigation" class="offCanvas print-hidden" id="offCanvas">
            <div class="offCanvas__navWrapper">
                <ul class="nav siteHeader__nav offCanvas__nav">
                    <Neos.Fusion:Loop items={props.items}>
                        <Neos.NeosIo:DefaultPage.Menu.Item item={item} />
                    </Neos.Fusion:Loop>
                </ul>
            </div>
        </nav>
    `

    @cache {
        mode = 'cached'
        entryIdentifier {
            site = ${Neos.Caching.entryIdentifierForNode(site)}
        }
        entryTags {
            pages = ${Neos.Caching.nodeTypeTag('Neos.Neos:Document', site)}
        }
    }
}

mainNavigation = Neos.NeosIo:DefaultPage.Menu.Renderer
