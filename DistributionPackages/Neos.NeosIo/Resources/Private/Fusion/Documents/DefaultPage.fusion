prototype(Neos.NeosIo:DefaultPage) < prototype(Neos.Neos:Page) {
    httpResponseHead {
        # https://scotthelme.co.uk/content-security-policy-an-introduction/
        headers.Content-Security-Policy = Neos.Fusion:Join {
            @glue = '; '
            object-src = "object-src 'self'"
            img-src = "img-src 'self' data: https://analytics.neos.io/ https://uptimerobot.com https://i.ytimg.com https://www.youtube-nocookie.com/ https://user-images.githubusercontent.com/ *.tile.openstreetmap.org/ https://raw.githubusercontent.com/ https://camo.githubusercontent.com/ https://m.neos.io/ https://www.gravatar.com/ https://img.youtube.com/"
            font-src = "font-src 'self' https://fonts.gstatic.com"
            script-src = "script-src 'self' 'unsafe-inline' https://analytics.neos.io/ https://www.youtube-nocookie.com/ https://www.google.com/ https://app.goessential.com https://m.neos.io/ https://slack.neos.io"
            style-src = "style-src 'self' 'unsafe-inline' data: https://www.youtube.com/ https://www.youtube-nocookie.com/ https://m.neos.io/"
            connect-src = "connect-src 'self' https://analytics.neos.io/"
            media-src = "media-src 'self'"
            base-uri = "base-uri 'self'"
            form-action = "form-action 'self' https://m.neos.io/"
            frame-ancestors = "frame-ancestors 'self'"
            frame-src = "frame-src 'self' https://www.youtube-nocookie.com/ https://www.youtube.com/ https://app.goessential.com/ https://analytics.neos.io/ https://m.neos.io/"
            report-uri = "report-uri /__csp-report"
        }

        # https://owasp.org/www-project-secure-headers/#div-headers
        headers.X-Frame-Options = 'sameorigin'
        headers.X-Content-Type-Options = 'nosniff'
        headers.Referrer-Policy = 'same-origin'
        headers.Permissions-Policy = 'camera=(), display-capture=(), geolocation=(), microphone=()'
        # disabled, see https://owasp.org/www-project-secure-headers/index.html#x-xss-protection
        headers.X-XSS-Protection = '0'
    }

    head {
        metaTags = Neos.NeosIo:MetaTags
        stylesheets = Neos.NeosIo:PageStyles
    }

    body {
        templatePath = 'resource://Neos.NeosIo/Private/Templates/Page/Default.html'
        sectionName = 'body'
        parts {
            siteHeader = Neos.NeosIo:Component.SiteHeader
            breadcrumb = Neos.NeosIo:DefaultPage.BreadcrumbMenu
            footer = Neos.NeosIo:Component.SiteFooter
        }

        homepage = ${site}

        content.keyVisual = Neos.NeosIo:KeyVisual {
            keyVisualImage = ${q(documentNode).property('keyVisualImage')}
            keyVisualImageSmall = ${q(documentNode).property('keyVisualImageSmall')}
            isKeyVisualContentsInverted = ${q(documentNode).property('isKeyVisualContentsInverted')}
        }

        javascripts.site = afx`
            <script @children="attributes.src" async="">
                <Neos.Fusion:ResourceUri path="resource://Neos.NeosIo/Public/Scripts/Main.js"/>
            </script>
        `
    }

    @cache.entryTags.site = ${Neos.Caching.nodeTag(site)}
}
