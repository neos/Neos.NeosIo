prototype(Neos.NeosIo:DocumentationPage) < prototype(Neos.NeosIo:DefaultPage) {
    body {
        header {
            keyVisual {
                content = Neos.Neos:ContentCollection {
                    nodePath = 'keyvisual'
                }
            }
            breadcrumb >
        }
        content {
            secondaryMenu = Neos.NeosIo:DocumentationPageSubMenu
            languages = Neos.Neos:DimensionsMenu {
                templatePath = 'resource://Neos.NeosIo/Private/Templates/FusionObjects/DocumentationPageDimensionsMenu.html'
                attributes.class = 'nav nav--language-switcher'
            }
            breadcrumb = Neos.NeosIo:DefaultPage.BreadcrumbMenu {
                attributes.class = 'nav nav--breadcrumb-ingrid'
            }

            prototype(Neos.NodeTypes:Text) {
                text.@process.replaceIcons = Neos.NeosIo:DocumentationContentIconProcessor
            }

            renderer >
            renderer = afx`
                <div class="stage stage--bright" id="mainContent">
                    <div class="stage__contents stage__contents--big">
                        <main class="content" role="main">
                            <div class="g">
                                <div class="gi u-w1/1 u-wm4/12 u-wl4/12">
                                    {props.secondaryMenu}
                                </div>
                                <div class="gi u-w1/1 u-wm8/12 u-wl8/12">
                                    <div class="g">
                                        <nav class="gi">
                                            {props.breadcrumb}
                                        </nav>
                                        <nav class="gi" style="float: right;">
                                            {props.languages}
                                        </nav>
                                    </div>
                                    {props.parts}
                                </div>
                            </div>
                        </main>
                    </div>
                </div>
            `
        }
    }
}
