prototype(Neos.Seo:OpenGraphMetaTags) {
    image {
        keyVisualImage {
            condition = ${q(documentNode).property('keyVisualImage')}
            renderer = ${q(documentNode).property('keyVisualImage')}
        }
    }
}

prototype(Neos.Seo:TwitterCard) {
    image.asset {
        keyVisualImage {
            condition = ${q(documentNode).property('keyVisualImage')}
            renderer = ${q(documentNode).property('keyVisualImage')}
        }
    }
}

// remove hreflang completely if the current node has "noindex" set
prototype(Neos.Seo:AlternateLanguageLinks) {
    @if.hasMetaRobotsNoIndexNotSet = ${q(this.node).property('metaRobotsNoindex') || q(this.node).is('[instanceof Neos.Seo:NoindexMixin]') ? false : true}
}

// remove link to translation if a target node has "noindex" set
prototype(Neos.Seo:AlternateLanguageLink) {
    @if.hasMetaRobotsNoIndexNotSet = ${q(this.node).property('metaRobotsNoindex') || q(this.node).is('[instanceof Neos.Seo:NoindexMixin]') ? false : true}
    @if.hasNoForeignCanonical = ${String.isBlank(q(this.node).property('canonicalLink'))}
}

prototype(Neos.Seo:CanonicalLink) {
    attributes.href.node = Neos.Fusion:Case {
        useFallback {
            condition = ${node.originDimensionSpacePoint != node.dimensionSpacePoint}
            renderer = ${q(node).context({dimensions: NeosIo.Node.dimensionSpacePointArray(node.originDimensionSpacePoint)}).get(0)}
        }
        default {
            condition = true
            renderer = ${node}
        }
    }
}
