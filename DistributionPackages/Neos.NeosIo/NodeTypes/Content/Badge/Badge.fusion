prototype(Neos.NeosIo:Component.Badge) < prototype(Neos.Fusion:Component) {
    image = ''
    highlight = false
    description = ''
    link = ''
    linkText = ''
    openInNewTab = false

    @propTypes {
        @strict = true
        image = ${PropTypes.string}
        highlight = ${PropTypes.boolean}
        description = ${PropTypes.string}
        link = ${PropTypes.string}
        linkText = ${PropTypes.string}
        openInNewTab = ${PropTypes.boolean}
    }

    @private {
        inBackend = ${renderingMode.isEdit}
        class = Neos.Fusion:DataStructure {
            main = 'badge neos-neosio-badge'
            highlight = ${'badge--highlighted'}
            highlight.@if.set = ${props.highlight}
        }
    }

    renderer = afx`
        <div class={private.class}>
            <Neos.Neos:ImageTag
                @if={props.image}
                asset={props.image}
                maximumWidth="360"
                attributes.class="badge__image"
                attributes.alt={"Logo of " + props.linkText + " badge"}
            />
            <img
                @if={private.inBackend && !props.image}
                class="badge__image"
                src={StaticResource.uri('Neos.Neos', 'Public/Images/dummy-image.svg')}
                title="Dummy image"
                alt="Dummy image"
            />

            <div @if.set={!String.isBlank(props.description)} class="badge__description">
                {props.description}
            </div>
            <Neos.Fusion:Tag
                tagName={props.link && !private.inBackend ? 'a' : 'div'}
                attributes.class="badge__linktext btn btn--solidPrimary"
                attributes.href={props.link}
                attributes.href.@if={props.link && !private.inBackend}
                attributes.target={props.openInNewTab ? '_blank' : null}
            >
                <span class="btn__content">
                    {props.linkText}
                </span>
            </Neos.Fusion:Tag>
        </div>

    `
}
