prototype(Neos.NeosIo:Component.FundingBadges) < prototype(Neos.Fusion:Component) {

    badgeData = null
    badgeFilterButton = ''

    @propTypes {
        @strict = true
        badgeData = ${PropTypes.any}
        badgeFilterButton = ${PropTypes.string}
    }

    @private {
        inBackend = ${renderingMode.isEdit}
    }

    // TODO use button component when merged
    renderer = afx`
        <div class="neos-neosio-fundingbadges" data-component="FundingBadges">
            <div class="fundingBadges__sortingControls" data-fake={props.badgeData.badgeTypes[0]}>
                <Neos.Fusion:Loop items={props.badgeData.badgeTypes} itemName="type" itemKey="label">
                    <span class="fundingBadges__sortingControl" data-filter={label}>
                        <Neos.NeosIo:Button
                            style={"btn--bright"}
                            buttonLabel={label}
                        />
                    </span>
                </Neos.Fusion:Loop>
            </div>
            <ul class="fundingBadges__list">
                <Neos.Fusion:Loop items={props.badgeData.customers} itemName="customer" itemKey="customerName">
                    <li
                        class="fundingBadges__singleElement"
                        data-customer-name={String.toLowerCase(customerName)}
                        data-filters={Json.stringify(customer.badgeTypes)}
                    >

                        <div class="funding-badge--customer-logo-wrap">
                            <img class="funding-badge--customer-logo" src={customer.logo} alt={"Logo of " + customerName} title={customerName}/>
                        </div>

                        <strong class="funding-badge--customer-name">
                            <a @if={customer.link} href={customer.link} title={"Visit " + customerName} target="_blank">
                                {customerName}
                            </a>
                            <span @if={!customer.link}>
                                {customerName}
                            </span>
                        </strong>

                        <ul>
                            <Neos.Fusion:Loop items={customer.badges} itemName="badge">
                                <li class="funding-badge--item">
                                    <div @if={badge.customerSum > 0 && badge.badgeCategory != 'Active Team Member Employers' && badge.badgeCategory != 'Sprint organizers & supporters'}>
                                        <strong class="funding-badge--customer-sum">
                                            {Number.formatCurrency(badge.customerSum)}
                                        </strong>
                                    </div>
                                    <Neos.NeosIo:Component.FundingBadges.StartEndDate startDateString={badge.startDate} endDateString={badge.endDate} />
                                    <small class="funding-badge--funding-type">
                                        {badge.fundingType} <span @if={badge.badgeType != badge.fundingType}>({badge.badgeType})</span>
                                    </small>
                                </li>
                            </Neos.Fusion:Loop>
                        </ul>
                    </li>
                </Neos.Fusion:Loop>
            </ul>
        </div>
    `
}

prototype(Neos.NeosIo:Component.FundingBadges.StartEndDate) < prototype(Neos.Fusion:Component) {
    startDateString = ''
    endDateString = ''

    @propTypes {
        @strict = true
        startDateString = ${PropTypes.string}
        endDateString = ${PropTypes.string}
    }

    @private {
        startDate = ${Date.create(props.startDateString)}
        endDate = ${Date.create(props.endDateString)}
    }

    renderer = afx`
        <small class="funding-badge--duration">
            {Date.format(private.startDate, "M d, Y")}
            <span @if={props.startDateString != props.endDateString}> - {Date.format(private.endDate, "M d, Y")}</span>
        </small>
    `
}
