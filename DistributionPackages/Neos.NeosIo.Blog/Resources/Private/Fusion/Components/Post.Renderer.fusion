prototype(Neos.NeosIo:PostRenderer) < prototype(Neos.Fusion:Component) {
    node = ${node}
    datePublished = ${q(node).property('datePublished')}
    title = ${q(node).property('title')}
    image = ${q(node).property('image')}
    summary = ${q(node).property('summary')}
    authors = ${q(node).property('authors')}
    authorAlternative = ${q(node).property('authorAlternative')}
    relatedDocuments = ${q(node).property('relatedDocuments')}
    author = ${this.authors ? Data.users(this.authors) + this.authorAlternative : this.authorAlternative}

    content = Neos.Neos:ContentCollection {
        nodePath = 'main'
    }

    @private {
        overviewUri = Neos.Neos:NodeUri {
            node = ${q(documentNode).parent().get(0)}
            absolute = true
        }
        latest = ${q(site).find('[instanceof Neos.NeosIo:Post]').remove(node).slice(0, 6).sort('datePublished', 'DESC').get()}
    }

    renderer = afx`
        <article class="post">
            <header>
                <h1 class="u-alignCenter">
                    <Neos.Neos:Editable property="title" block={false} />
                </h1>

                <div class="u-alignCenter">
                    <Neos.Neos:Editable property="summary" />
                </div>
                <p class="u-alignCenter u-microCopy">
                    <time datetime={Date.format(props.datePublished, 'c')}>
                        {Date.formatCldrDate(props.datePublished)}
                    </time>
                    <span @if={props.author}>
                        &nbsp;â€“ Written by <span>{props.author}</span>
                    </span>
                </p>
                <hr />
            </header>

            <main class="postBody">
                {props.content}
            </main>

            <footer class="post__footer">
                <hr />
                <section class="u-mt1/1">
                    <div class="u-pt1/1" @if={props.relatedDocuments}>
                        <h3>Related</h3>
                        <ul class="nav nav--stacked u-mb1/2">
                            <Neos.Fusion:Loop items={props.relatedDocuments} itemName="relatedDocument">
                                <li class="u-mb1/2">
                                    <Neos.Neos:NodeLink node={relatedDocument} />
                                </li>
                            </Neos.Fusion:Loop>
                        </ul>
                    </div>

                    <div class="u-pt1/1">
                        <h3>Latest Posts</h3>
                        <ul class="nav nav--stacked u-mb1/2">
                            <Neos.Fusion:Loop items={private.latest} itemName="post">
                                <li class="u-mb1/2" title={String.stripTags(post.properties.summary)}>
                                    <div class="h5">
                                        {Date.formatCldrDate(post.properties.datePublished)}
                                        -
                                        <Neos.Neos:NodeLink node={post} />
                                    </div>
                                </li>
                            </Neos.Fusion:Loop>
                        </ul>

                        <div class="u-alignRight">
                            <Neos.NeosIo:Component.ClassicButton
                                color="btn--solidPrimary"
                                uri={private.overviewUri}
                            >
                                Back to list
                            </Neos.NeosIo:Component.ClassicButton>
                        </div>
                    </div>
                </section>
            </footer>
        </article>
    `
}
