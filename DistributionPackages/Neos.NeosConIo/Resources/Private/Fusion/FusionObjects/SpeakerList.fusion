prototype(Neos.NeosConIo:SpeakerList) < prototype(Neos.Neos:ContentComponent) {
    items = ${q(node).referenceNodes('eventToShowSpeakersFor').backReferenceNodes("event").filter('[instanceof Neos.NeosConIo:Speaker]').sort('title', 'ASC')}

    renderer = afx`
        <ul class="speaker__speakerList">
            <Neos.Fusion:Loop items={props.items} itemName="node">
                <li class="speaker__speakerList__item">
                    <Neos.NeosIo:ImageTeaser
                        subTitle=""
                        title={q(node).property('title')}
                        image={q(node).property('image')}
                        text={String.stripTags(q(node).property('company'))}
                        className="imageTeaser--speaker"
                    >
                        <Neos.Neos:NodeUri node={node} absolute={true} @path="link" />
                    </Neos.NeosIo:ImageTeaser>
                </li>
            </Neos.Fusion:Loop>
        </ul>
    `

    @cache {
        mode = 'cached'
        entryIdentifier {
            node = ${Neos.Caching.entryIdentifierForNode(node)}
        }
        entryTags {
            node = ${Neos.Caching.nodeTag(node)}
            events = ${Neos.Caching.nodeTypeTag('Neos.NeosConIo:Event', node)}
            speakers = ${Neos.Caching.nodeTypeTag('Neos.NeosConIo:Speaker', node)}
        }
    }
}
