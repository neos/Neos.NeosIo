prototype(Neos.NeosConIo:SpeakerList) < prototype(Neos.Fusion:Collection) {
    // API to the outside: "collection" to render
    collection = ${q(site).find('[instanceof Neos.NeosConIo:Speaker]').arrayPropertyContains("event", node.properties.eventToShowSpeakersFor)}

    @context.eventToFilter = ${node.properties.eventToShowSpeakersFor}
    @process.wrapWithUl = ${'<ul class="speaker__speakerList">' + value + '</ul>'}


    @cache {
        mode = 'cached'
        entryIdentifier {
            node = ${node}
        }
        tags {
            1 = ${Neos.Caching.nodeTag(node)}
            1 = ${Neos.Caching.nodeTypeTag('Neos.NeosConIo:Talk')}
            1 = ${Neos.Caching.nodeTypeTag('Neos.NeosConIo:Event')}
            1 = ${Neos.Caching.nodeTypeTag('Neos.NeosConIo:Room')}
            1 = ${Neos.Caching.nodeTypeTag('Neos.NeosConIo:Speaker')}
        }
    }


    itemName = "node"
    itemRenderer = Neos.Fusion:Tag {
        tagName = 'li'
        attributes.class = 'speaker__speakerList__item'
        content = Neos.NeosIo:ImageTeaser {
            // node is the speaker here
            subTitle = ""
            title = ${node.properties.title}
            image = ${node.properties.image}
            text = ${String.stripTags(node.properties.company)}

            // Ensure Text has no tags (especially link tag) in the markup, as this
            // will otherwise lead to two nested <a> tags which makes the browser break.
            text.@process.stripTags = ${String.stripTags(value)}
            className = 'imageTeaser--speaker'
            link = Neos.Neos:NodeUri {
                node = ${node}
                absolute = true
            }
        }
    }
}