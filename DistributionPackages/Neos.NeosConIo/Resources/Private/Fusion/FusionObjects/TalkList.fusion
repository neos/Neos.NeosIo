prototype(Neos.NeosConIo:TalkList) < prototype(Neos.Fusion:Collection) {
    // API to the outside: "collection" to render
    collection = ${q(site).find('[instanceof Neos.NeosConIo:Talk]')}

    @context.eventToFilter = ${node.properties.eventToShowTalksFor}
    @process.wrapWithUl = ${'<ul class="talkList">' + value + '</ul>'}
    itemName = "node"
    itemRenderer = Neos.Fusion:Tag {
        tagName = 'li'
        attributes.class = 'talkList__item'
        content = Neos.NeosIo:ImageTeaser {
            // node is the talk here
            title = ${node.properties.title}
            image = ${node.properties.thumbnail}
            fallbackImageUrl = ${(node.properties.video && !node.properties.thumbnail) ? "https://img.youtube.com/vi/" + node.properties.video + "/maxresdefault.jpg" : null}
            text = ${node.properties.text}
            className = 'imageTeaser--relatedTalk'
            link = Neos.Neos:NodeUri {
                node = ${node}
                absolute = true
            }
        }

    }
    // "node" is the current talk here
    itemRenderer.@if.talkHasCorrectEvent = ${node.properties.event == eventToFilter}

    @cache {
        mode = 'cached'
        entryTags {
            # when a talk is changed, we need to rerender this page.
            1 = ${Neos.Caching.nodeTypeTag('Neos.NeosConIo:Talk')}
        }
    }
}
