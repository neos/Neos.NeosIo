prototype(Neos.NeosConIo:Talk) < prototype(Neos.NeosIo:DefaultPage) {
    header.keyVisualWrapper.keyVisual >

    content.parts.main = Neos.Neos:ContentComponent {
        renderer = Neos.NeosConIo:Component.Talk {
            title = Neos.Neos:Editable {
                property = 'title'
            }

            text = Neos.Neos:Editable {
                property = 'text'
            }

            video = Neos.NeosIo:Youtube {
                video = ${node.properties.video}
            }

            speakerList = Neos.NeosConIo:SpeakerList {
                @if.hasSpeakers = ${!Array.isEmpty(q(node).referenceNodes('speakers'))}
                items = ${q(node).referenceNodes('speakers')}
            }

            eventTitle = ${q(node).referenceNodes('event').property('title')}

            talkDate = ${node.properties.talkDate}

            roomName = ${q(node).referenceNodes('room').property('name')}

            isTalkListVisible = ${!Array.isEmpty(q(node).referenceNodes('relatedTalks'))}
            talkList = Neos.NeosConIo:TalkList {
                items = ${q(node).referenceNodes('relatedTalks')}
                itemRenderer.@if.talkHasCorrectEvent = true
            }
        }
    }
}
