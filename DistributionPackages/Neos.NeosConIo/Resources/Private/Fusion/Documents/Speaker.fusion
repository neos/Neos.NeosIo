prototype(Neos.NeosConIo:Speaker) < prototype(Neos.NeosIo:DefaultPage) {
    header.keyVisualWrapper.keyVisual >

    content.parts.main = Neos.Neos:ContentComponent {
        renderer = Neos.NeosConIo:Component.Speaker {
            title = Neos.Neos:Editable {
                property = "title"
            }

            text = Neos.Neos:Editable {
                property = "text"
            }

            imageUri = Neos.Neos:ImageUri {
                asset = ${q(node).property('image')}
                allowCropping = true
            }

            company = ${node.properties.company}
            position = ${node.properties.position}
            twitter = ${node.properties.twitter}
            mastodon = ${node.properties.mastodon}
            github = ${node.properties.github}

            // node is the speaker here
            @context.featuredTalkNode = ${q(node).referenceNodes('featuredTalk').get(0)}

            featuredTalk = Neos.NeosIo:ImageTeaser {
                title = ${q(featuredTalkNode).property('title')}
                rawTitle = ${this.title}
                image = ${q(featuredTalkNode).property('thumbnail')}
                fallbackImageUrl = ${(q(featuredTalkNode).property('video') && !q(featuredTalkNode).property('thumbnail')) ? "https://img.youtube.com/vi/" + q(featuredTalkNode).property('video') + "/maxresdefault.jpg" : null}
                text = ${String.cropAtWord(q(featuredTalkNode).property('text'), 200, 'â€¦')}
                className = 'imageTeaser--featuredTalk'
                link = Neos.Neos:NodeUri {
                    node = ${featuredTalkNode}
                    absolute = true
                }
            }

            // Just show featured talk when defined in the backend
            featuredTalk.@if.featuredTalkIsSelected = ${featuredTalkNode}

            moreTalks = Neos.Fusion:Loop {
                // node is speaker here
            items = ${q(node).backReferenceNodes("speakers").filter('[instanceof Neos.NeosConIo:Talk]').sort('talkDate', 'DESC')}

                @process.wrapWithUl = ${value ? '<ul class="talkList">' + value + '</ul>' : ''}
                itemName = "node"
                itemRenderer = Neos.Fusion:Tag {
                    tagName = 'li'
                    attributes.class = 'talkList__item'
                    content = Neos.NeosIo:ImageTeaser {
                        // node is the talk here
                    title = ${q(node).property('title')}
                        image = ${q(node).property('thumbnail')}
                        fallbackImageUrl = ${(q(node).property('video') && !q(node).property('thumbnail')) ? "https://img.youtube.com/vi/" + q(node).property('video') + "/maxresdefault.jpg" : null}
                        text = ''
                        className = 'imageTeaser--relatedTalk'
                        link = Neos.Neos:NodeUri {
                            node = ${node}
                            absolute = true
                        }
                    }
                }

                itemRenderer.@if.talkHasCorrectSpeaker = true
                itemRenderer.@if.talkIsNotFeatured = ${node.aggregateId.value != featuredTalkNode.aggregateId.value}
            }
        }
    }
}
