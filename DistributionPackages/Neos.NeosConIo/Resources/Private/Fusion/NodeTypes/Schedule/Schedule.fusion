prototype(Neos.NeosConIo:Schedule) < prototype(Neos.Neos:ContentComponent) {
    @if.eventNodeIsSet = ${q(event).is('[instanceof Neos.NeosConIo:Event]')}
    @if.dateIsSet = ${q(q(event).property('eventDate')).is('[instanceof DateTime]')}

    @context {
        event = ${q(node).property('event')}
        days = Neos.Fusion:RawArray {
           day1 = ${q(node).property('event') && q(q(node).property('event')).property('eventDate')}
           day2 = ${q(node).property('event') && q(q(node).property('event')).property('eventDate') && Date.add(q(q(node).property('event')).property('eventDate'), 'P1D')}
        }
    }

    @cache {
        mode = 'cached'
        entryIdentifier {
            node = ${node}
        }
        tags {
            1 = ${Neos.Caching.nodeTag(node)}
            2 = ${Neos.Caching.nodeTypeTag('Neos.NeosConIo:Talk')}
            3 = ${Neos.Caching.nodeTypeTag('Neos.NeosConIo:Event')}
            4 = ${Neos.Caching.nodeTypeTag('Neos.NeosConIo:Room')}
            5 = ${Neos.Caching.nodeTypeTag('Neos.NeosConIo:Speaker')}
            6 = ${Neos.Caching.nodeTypeTag('Neos.NeosConIo:BreakInSchedule')}
        }
    }

    days = ${days}

    renderer = afx`
        <div class="schedule" data-component="Schedule">
            <Neos.Fusion:Collection collection={props.days} itemName="day" @children="itemRenderer">
                <h3 class="conferenceDayTitle">Conference Day {iterator.cycle}</h3>
                <div class="conferenceDaySubtitle">
                    {Date.format(day, 'd M Y')}
                </div>
                <div class="talks">
                    <Neos.NeosConIo:Schedule.Talks day={day} />
                </div>
            </Neos.Fusion:Collection>
        </div>
    `
}