prototype(Neos.Presentation:Background) < prototype(Neos.Fusion:Component) {
    @styleguide.props {
        background = 'gradient'
        content = 'Lorem ipsum dolor sit amet, consectetur adipisicing elit. Assumenda atque cumque, dignissimos ea eos error in iure laboriosam libero mollitia nam nemo omnis, perferendis quisquam recusandae reiciendis repudiandae unde ut!'
    }

    @propTypes {
        background = ${PropTypes.oneOf(['gradient', 'blue-dark', 'blue', 'blue-light', 'purple', 'purple-dark', 'green', 'green-dark', 'yellow', 'yellow-dark', 'orange', 'red', 'black'])}
        tagName = PropTypes:String
        style = PropTypes:String
        href = PropTypes:String
        class = ${PropTypes.any}
    }

    tagName = 'div'
    class = 'flow-root'

    attributes = Neos.Fusion:DataStructure

    // Extension point for pixel backgrounds
    @private {
        class = ''
        attributes = Neos.Fusion:DataStructure
    }

    renderer = Neos.Fusion:Tag {
        tagName = ${props.href ? 'a' : props.tagName}
        content = ${props.content}
        attributes = afx`
            <Neos.Fusion:DataStructure
                {...props.attributes}
                {...private.attributes}
                style={props.style}
                href={props.href}
                class={Carbon.String.classNames(
                    'n-background',
                    props.background && 'n-background--' + props.background,
                    props.background == 'gradient' && 'bg-linear-to-r from-blue-dark to-blue',
                    private.class,
                    props.class
                )}
            />
        `
    }
}
