prototype(Neos.Presentation:Carousel) < prototype(Neos.Fusion:Component) {
    @styleguide {
        title = 'Carousel'
        props {
            headline = 'A nice looking carousel!'
            cards = Neos.Fusion:Map {
                items = ${['one', 'two', 'three', 'four']}
                itemRenderer = Neos.Fusion:DataStructure {
                    headline = ${'Card ' + item + ' headline'}
                    text = ${'Card ' + item + ' text'}
                    button {
                        content = ${'Card ' + item + ' link'}
                        href = '#'
                    }
                }
            }
        }
    }

    @private {
        cards = Neos.Fusion:Map {
            items = ${props.cards}
            itemRenderer = afx`
                <Neos.Presentation:Molecule.Carousel.Card {...item} />
            `
        }
    }

    options = Neos.Fusion:DataStructure {
        perPage = 3
        perMove = 1
        gap = '1rem'
        padding = '1rem'
    }

    renderer = afx`
        <div class="grid grid-cols-4 mt-8">
            <div class="flex flex-col col-span-1 p-8 rounded-xl shadow-lg h-full">
                <Neos.Presentation:Headline class="" content={props.headline} display="headline-lg"/>
            </div>
            <Neos.Presentation:Slider trackClass="py-4 h-full" slideItemClass="flex flex-col justify-between items-start p-8 rounded-xl shadow-lg" class="col-span-3 h-full" {...props} items={private.cards} />
        </div>
    `
}
