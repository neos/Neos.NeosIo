prototype(Neos.Presentation:Footer) < prototype(Neos.Fusion:Component) {
    @styleguide {
        title = 'Footer'
        description = 'Footer of the page'
        props {
            linkGroups = Neos.Fusion:Map {
                items = ${Array.range(1, 4)}
                itemRenderer = Neos.Fusion:DataStructure {
                    title = 'Features'
                    links = Neos.Fusion:Map {
                        items = ${Array.range(1, 5)}
                        itemRenderer = Neos.Fusion:DataStructure {
                            title = 'Why Neos'
                            link = '#'
                        }
                    }
                }
            }

            socials = Neos.Fusion:DataStructure {
                facebook {
                    link = 'https://www.facebook.com/neoscms'
                    icon = 'facebook'
                    label = 'Facebook'
                }
                github {
                    link = 'https://github.com/neos/neos'
                    icon = 'github'
                    label = 'Github'
                }
                linkedin {
                    link = 'https://www.linkedin.com/company/neoscms'
                    icon = 'linkedin'
                    label = 'LinkedIn'
                }
                youtube {
                    link = 'https://www.youtube.com/channel/UCQp8FVUGr9gGZbU8ZgN6aaw'
                    icon = 'youtube'
                    label = 'YouTube'
                }
                slack {
                    link = 'https://slack.neos.io/'
                    icon = 'slack'
                    label = 'Slack'
                }
                mastodon {
                    link = 'https://mastodon.neos.io/'
                    icon = 'mastodon'
                    label = 'Mastodon'
                }
            }

            endlinks = Neos.Fusion:DataStructure {
                imprint {
                    link = 'https://www.neos.io/imprint.html'
                    label = 'Imprint'
                }
                privacy {
                    link = 'https://www.neos.io/privacy.html'
                    label = 'Privacy'
                }
                terms {
                    link = 'https://www.neos.io/terms.html'
                    label = 'Terms'
                }
            }
        }
    }

    @propTypes {
        // does not work with monocle yet
        //linkGroups = PropTypes:Array {
        //    type = PropTypes:DataStructure {
        //        title = PropTypes:String
        //        links = PropTypes:Array {
        //            type = Neos.Fusion:DataStructure {
        //                title = PropTypes:String
        //                link = PropTypes:String
        //            }
        //        }
        //    }
        //}

        socials = PropTypes:Array {
            type = PropTypes:DataStructure {
                link = PropTypes:String
                icon = PropTypes:String
                label = PropTypes:String
            }
        }

        endlinks = PropTypes:Array {
            type = PropTypes:DataStructure {
                link = PropTypes:String
                label = PropTypes:String
            }
        }
    }

    renderer = afx`
      <Neos.Presentation:Background background="gradient" tagName="footer">
        <Neos.Presentation:Spacing y>
          <Neos.Presentation:Spacing x y="small" size>
            <div class="flex flex-wrap gap-12 mb-10">
              <div class="grow grid gap-y-14">
                <Neos.Presentation:Icon icon="neosAvatarMonochrome" class="h-12" />
                <Neos.Presentation:Paragraph
                  content="Neos is free and open source software licensed under GPL v3 or later. Created with passion by over 100 contributors around the world."
                  class="max-w-screen-xxs"
                />
              </div>
              <nav class="flex grow-3 flex-wrap gap-8" @if={Carbon.Array.check(props.linkGroups)}>
                <Neos.Fusion:Loop items={props.linkGroups} itemName="group">
                  <div class="grow flex flex-col gap-y-1" @if={Carbon.Array.check(group.links)}>
                    <Neos.Presentation:Headline display="title-md" class="mb-3" content={group.title} />
                    <Neos.Fusion:Loop items={group.links}>
                        <a
                            href={item.link}
                            class="inline-block py-1 underline-offset-8 display-body-lg hocus:underline"
                        >
                            {item.title}
                        </a>
                    </Neos.Fusion:Loop>
                  </div>
                </Neos.Fusion:Loop>
              </nav>
            </div>
          </Neos.Presentation:Spacing>
          <Neos.Presentation:Spacing x y="small" size @if={Carbon.Array.check(props.socials)}>
            <nav class="flex flex-wrap gap-4 -mx-2" x-tooltips x-data>
              <Neos.Fusion:Loop items={props.socials}>
                <a
                    href={item.link}
                    aria-label={item.label}
                    class="flex items-center justify-center p-2 border border-transparent transition-colors rounded-full hocus:border-current"
                >
                    <Neos.Presentation:Icon icon={item.icon} class="size-6" />
                </a>
              </Neos.Fusion:Loop>
            </nav>
          </Neos.Presentation:Spacing>
          <Neos.Presentation:Spacing x y="small" size @if={Carbon.Array.check(props.endlinks)}>
            <nav class="flex flex-wrap gap-6">
              <Neos.Fusion:Loop items={props.endlinks}>
                <a
                    href={item.link}
                    class="underline-offset-8 hocus:underline"
                >
                    {item.label}
                </a>
              </Neos.Fusion:Loop>
            </nav>
          </Neos.Presentation:Spacing>
        </Neos.Presentation:Spacing>
      </Neos.Presentation:Background>
    `
}
