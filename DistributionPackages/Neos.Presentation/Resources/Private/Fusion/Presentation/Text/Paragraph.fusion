prototype(Neos.Presentation:Paragraph) < prototype(Neos.Fusion:Component) {
    @styleguide.props.content = 'Lorem ipsum dolor <em>sit amet</em>, consetetur sadipscing elitr, sed diam nonumy eirmod tempor invidunt ut labore et dolore magna aliquyam erat, sed diam voluptua. At vero eos et accusam et justo duo dolores et ea rebum. Stet clita kasd gubergren, no sea takimata sanctus est <strong>Lorem ipsum</strong> dolor sit amet. Lorem ipsum dolor sit amet, consetetur sadipscing elitr, sed diam nonumy eirmod tempor invidunt ut labore et dolore magna aliquyam erat, sed diam voluptua. At vero eos et accusam et justo duo dolores et ea rebum. Stet clita kasd gubergren, no sea takimata sanctus est Lorem ipsum dolor sit amet.'

    @propTypes {
        content = PropTypes:String
        tagName = ${PropTypes.oneOf(['p'])}
        display = ${PropTypes.oneOf([
            'tagname',
            'headline-xl',
            'headline-lg',
            'headline-md',
            'headline-sm',
            'title-lg',
            'title-md',
            'title-sm',
            'lead',
            'body-lg',
            'body-md',
            'body-sm',
            'label-lg',
            'label-md',
            'label-sm',
            'cta-xl',
            'cta-lg',
            'cta-sm'
        ])}
    }

    tagName = 'p'

    // By default we use the defined tagname
    display = 'tagname'

    @private {
        display = ${!Type.isString(props.display) && !Type.isInteger(props.display) ? 'tagname' : props.display}
        additionalClass = 'n-paragraph'
    }
    @if.hasContent = ${this.content}

    renderer = Neos.Fusion:Tag {
        tagName = ${props.tagName}
        attributes = Neos.Fusion:DataStructure {
            @apply.attributes = ${props.attributes}
            class = ${Carbon.String.classNames(
                'display-' + private.display,
                private.additionalClass,
                props.class
            )}
            style = ${props.style}
        }

        content = ${props.content}
    }
}
