prototype(Neos.Presentation:Button) < prototype(Neos.Fusion:Component) {
    @styleguide {
        props.content = 'Button Text'
        useCases.icon.props {
            content = 'Button with Icon'
            icon = 'animatedArrow'
        }
    }

    @propTypes {
        variant = ${PropTypes.oneOf(['button', 'link'])}
        type = ${PropTypes.oneOf(['button', 'submit', 'reset'])}
        content = PropTypes:String
        ariaLabel = PropTypes:String
        icon = PropTypes:String
        disabled = PropTypes:Bool
        class = ${PropTypes.any}
    }

    variant = 'button'
    type = 'button'
    attributes = Neos.Fusion:DataStructure

    @private.class = ${Carbon.String.classNames(
        'n-button n-interactive',
        'n-interactive--' + props.variant,
        {
            'n-interactive--disabled cursor-not-allowed': !!props.disabled,
            'group': !!(props.icon == 'animatedArrow' || props.icon == 'animatedArrowMoveRight')
        },
        props.class
    )}

    @if.hasLabelOrContent = ${this.content || this.ariaLabel}

    renderer = afx`
        <button
            {...props.attributes}
            disabled={props.disabled}
            aria-label={props.ariaLabel}
            type={props.type}
            class={private.class}
        >
            {props.content}{props.icon ? ' ' : ''}
            <Neos.Presentation:Icon icon={props.icon} @if={props.icon} />
        </button>
    `
}
