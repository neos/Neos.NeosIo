prototype(Neos.Presentation:Module.PersonCards) < prototype(Neos.Fusion:Component) {
    @styleguide.props {
        label = 'People behind Neos CMS'
        headline = 'Open Source, Open Minds'
        cards = Neos.Fusion:DataStructure {
            0 {
                name = "Karsten Dambekalns"
                email = "karsten@neos.io"
                background = "gradient"
                imageSource = Sitegeist.Kaleidoscope:UriImageSource {
                    uri = 'https://images.unsplash.com/photo-1472099645785-5658abf4ff4e?auto=format&fit=facearea&facepad=5&ixlib=rb-4.0.3&ixid=M3wxMjA3fDB8MHxwaG90by1wYWdlfHx8fGVufDB8fHx8fA%3D%3D'
                }
            }
            1 {
                name = "Anke Häslich"
                email = "anke@neos.io"
                background = "purple"
                imageSource = Sitegeist.Kaleidoscope:UriImageSource {
                    uri = 'https://images.unsplash.com/photo-1517841905240-472988babdf9?auto=format&crop=faces&fp-y=0.8&fp-z=2&w=1024&h=1024&q=80'
                }
            }
            2 {
                name = "Christian Müller"
                email = "christian@neos.io"
                background = "green"
                imageSource = Sitegeist.Kaleidoscope:UriImageSource {
                    uri = 'https://images.unsplash.com/photo-1521119989659-a83eee488004?auto=format&crop=faces&fp-y=0.8&fp-z=2&w=1024&h=1024&q=80'
                }
            }
        }
    }

    @propTypes {
        headline = PropTypes:String
        cards = PropTypes:Array {
            type = PropTypes:DataStructure {
                name = PropTypes:String
                email = PropTypes:String
                imageSource = ${PropTypes.instanceOf('\\Sitegeist\\Kaleidoscope\\Domain\\ImageSourceInterface')}
            }
        }
    }

    headline = null
    cards = null

    @if.hasCards = ${Type.isArray(this.cards) && Array.length(this.cards)}

    renderer = afx`
        <Neos.Presentation:Spacing size x y>
            <Neos.Presentation:Spacing y="small" @if={props.headline} tagName="h2" class="display-headline-lg text-center">
                <small @if={props.label} class="block display-cta-sm">{props.label}</small>
                {props.headline}
            </Neos.Presentation:Spacing>
            <Neos.Presentation:Spacing y="small">
                <ul role="list" class="grid sm:grid-cols-2 md:grid-cols-3 items-stretch gap-6 text-white">
                    <Neos.Fusion:Loop items={props.cards}>
                        <li class="flex flex-col @container">
                            <figure>
                                <Sitegeist.Kaleidoscope:Image imageSource={item.imageSource} attributes.class="aspect-4/5 w-full object-cover"/>
                            </figure>
                            <Neos.Presentation:Background background={item.background} pixelPosition="top" pixelCycle={iterator.cycle} class="px-8 pb-8 flex-1 flex flex-col">
                                <Neos.Presentation:Headline content={item.name} tagName="h3" display="title-lg" class="flex-1 pt-8" />
                                <a href={"mailto:" + item.email}>
                                    <span class="sr-only">{I18n.translate('Neos.Presentation:Main:email')}</span>
                                    <Neos.Presentation:Icon icon="email" class="w-5 inline-block mr-2"/>
                                    {item.email}
                                </a>
                            </Neos.Presentation:Background>
                        </li>
                    </Neos.Fusion:Loop>
                </ul>
            </Neos.Presentation:Spacing>
        </Neos.Presentation:Spacing>
    `
}
