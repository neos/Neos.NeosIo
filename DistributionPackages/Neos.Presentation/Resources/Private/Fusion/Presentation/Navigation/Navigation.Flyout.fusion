prototype(Neos.Presentation:Navigation.Flyout) < prototype(Neos.Fusion:Component) {

    @propTypes {
        flyoutVariant = ${PropTypes.oneOf(['default', 'white'])}
    }

    flyoutVariant = 'default'

    @private {
        isDefault = ${props.flyoutVariant == 'default'}
        isWhite = ${props.flyoutVariant == 'white'}
    }

    renderer = afx`
        <div @if={props.flyoutItems && props.flyoutItems != []} class="absolute hidden group-hover/navitem:block top-full left-0 right-0 pt-spacing-y-small" x-bind:class="{'!block': open}">
            <div class={Carbon.String.classNames('flow-root fade-in slide-up', {
                'bg-blue-dark backdrop-blur-3xl': private.isDefault,
                'bg-white text-blue-dark': private.isWhite
            })}>
                <Neos.Presentation:Spacing y x>
                    <div class="grid grid-cols-3 gap-6 fade-in slide-up">
                        <Neos.Fusion:Loop items={props.flyoutItems} itemName="flyoutItem">
                            <div @if={!flyoutItem.body}>
                                <Neos.Presentation:Headline content={flyoutItem.title} display="title-md" class={Carbon.String.classNames("mb-8", {
                                    'text-blue-light': private.isDefault,
                                    'text-blue-light ': private.isWhite
                                })} />
                                <ul class="grid gap-1">
                                    <Neos.Fusion:Loop items={flyoutItem.items} itemName="item">
                                        <li class="group/li">
                                            <a href={item.link} class={Carbon.String.classNames("flex px-5 py-3 transition -translate-x-4 group-hover/li:translate-x-0",{
                                                'group-hover/li:bg-blue-light/5': private.isDefault,
                                                'group-hover/li:bg-blue-dark/15': private.isWhite
                                            })}>
                                                <div class="flex transition -translate-x-1 group-hover/li:translate-x-0 gap-4 ">
                                                    <Neos.Presentation:Icon icon={item.icon} class="w-5" />
                                                    <span class="grid gap-1">
                                                        <strong class="display-label-lg">{item.label}</strong>
                                                        <span class="display-body-md">{item.description}</span>
                                                    </span>
                                                </div>
                                            </a>
                                        </li>
                                    </Neos.Fusion:Loop>
                                </ul>
                            </div>
                            <div @if={flyoutItem.body} class="self-end">
                                {flyoutItem.body}
                            </div>
                        </Neos.Fusion:Loop>
                    </div>
                </Neos.Presentation:Spacing>
            </div>
        </div>
    `
}
