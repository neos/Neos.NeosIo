prototype(Neos.Presentation:Icon) < prototype(Neos.Fusion:Component) {
    @styleguide.props {
        icon = 'clock'
        class = 'group/icon'
        style = 'font-size: 90px; margin: 1em auto; color: var(--color-blue)'
        sizeClass = 'h-[1em]'
    }

    @propTypes {
        icon = ${PropTypes.oneOf([
            'animatedArrow', 'animatedArrowLeft',
            'neosAvatarPrimary', 'neosAvatarLight', 'neosAvatarDark', 'neosAvatarMonochrome', 'neosLogoPrimary', 'neosLogoLight', 'neosLogoDark', 'neosLogoMonochrome',
            'arrow-right', 'briefcase', 'calculator', 'calendar', 'chevron-down', 'chevron-left', 'chevron-right', 'chevron-up', 'clock', 'cloud', 'cursor', 'desktop',
            'development', 'document', 'eye', 'facebook', 'file', 'github', 'globe', 'headphones', 'heart', 'house', 'instagram', 'linkedin', 'mail-open',
            'mail', 'mastodon', 'place', 'quote-left', 'plus', 'quote', 'search', 'slack', 'smile', 'square', 'time', 'user', 'youtube', 'notfound'
        ])}
        ariaLabel = PropTypes:String
        class = ${PropTypes.any}
        sizeClass = ${PropTypes.any}
        style = PropTypes:String
    }

    icon = null
    sizeClass = ${this.icon == 'animatedArrow' || this.icon == 'animatedArrowLeft' ? 'h-[1em]' : 'h-6'}
    style = null
    ariaLabel = null
    ariaHidden = null

    @private {
        icon = ${props.icon ? String.trim(props.icon) : null}
        neosColorFirst = Neos.Fusion:Match {
            @subject = ${private.icon}
            @default = 'fill-blue-dark'
            neosAvatarDark = 'fill-blue-light'
            neosLogoDark = 'fill-blue-light'
        }
        neosColorSecond = Neos.Fusion:Match {
            @subject = ${private.icon}
            @default = 'fill-white'
            neosAvatarPrimary = 'fill-blue-light'
            neosLogoPrimary = 'fill-blue-light'
        }
        class = ${Tailwind.merge(
            private.icon == 'animatedArrow' && 'transition-transform duration-300 ease-[steps(5)] group-hocus-within/icon:translate-x-1/2',
            private.icon == 'animatedArrowLeft' && 'rotate-180 transition-transform duration-300 ease-[steps(5)] group-hocus-within/icon:-translate-x-1/2',
            props.sizeClass,
            props.class
        )}
        neosAvatar = afx`
            <svg xmlns="http://www.w3.org/2000/svg" viewBox="0 0 111.73 124.72">
                <polygon class={private.neosColorFirst} points="89.03 0 68.27 15.13 68.27 46.77 89.03 76.12 89.03 0"/>
                <polygon class={private.neosColorFirst} points="89.03 112.82 9.24 0 0 6.76 0 124.72 20.75 109.6 20.75 51.18 72.65 124.72 95.35 124.72 111.73 112.82 89.03 112.82"/>
                <polygon class={private.neosColorSecond} points="20.75 51.18 20.75 109.6 0 124.72 22.7 124.72 43.46 109.6 43.46 83.35 20.75 51.18"/>
                <polygon class={private.neosColorSecond} points="89.03 76.12 89.03 0 111.73 0 111.73 112.82 89.03 112.82 9.24 0 35.19 0 89.03 76.12"/>
            </svg>
        `
        neosLogo = afx`
            <svg xmlns="http://www.w3.org/2000/svg" xml:space="preserve" viewBox="0 0 453.5 124.7">
                <path class={private.neosColorFirst} d="m410.7 86 5-5.5c5.5 4.9 10.6 7.5 18.1 7.5 6.7 0 11.5-3.4 11.5-8.5 0-4.5-2.5-6.9-13.5-9.7-12.7-3.2-19.1-7.6-19.1-16.5 0-9.2 8.5-15.5 19.3-15.5 8.1 0 14 2.6 19.7 7.1l-4.6 5.9c-5.3-4.1-9.7-5.9-15.2-5.9-6.1 0-10.9 3-10.9 7.8 0 4.6 3 7.1 14.3 9.9 12.2 2.9 18.2 7.8 18.2 16.3 0 10-8.6 16.2-19.9 16.2-8.9.1-16.3-3.1-22.9-9.1zm-82.9-19.5c0-15.7 12.1-28.7 29-28.7s29 13 29 28.7-12.1 28.7-29 28.7-29-13-29-28.7zm49.7 0c0-12.1-8.5-21.3-20.7-21.3s-20.7 9.2-20.7 21.3 8.5 21.3 20.7 21.3 20.7-9.1 20.7-21.3zm-75-27.7V46h-32.7v16.7H299V70h-29.2v17.1h33.1v7.2h-41.2V38.8h40.8zm-111.4 0 32.5 40.9V38.8h7.9v55.4h-6.6l-33.4-42.1v42.1h-7.9V38.8h7.5zM89 0 68.3 15.1v31.7L89 76.1zm0 112.8L9.2 0 0 6.8v117.9l20.8-15.1V51.2l51.9 73.5h22.6l16.4-11.9z" />
                <path class={private.neosColorSecond} d="M20.8 51.2v58.4L0 124.7h22.7l20.8-15.1V83.3zM89 76.1V0h22.7v112.8H89L9.2 0h26z" />
            </svg>
        `
        animatedArrow = afx`
            <svg
                xmlns="http://www.w3.org/2000/svg"
                viewBox="0 0 5 5"
                fill="currentColor"
            >
                <path class="transition-transform ease-[steps(5)] scale-x-0 group-hocus-within/icon:scale-x-100 duration-300 origin-right motion-reduce:hidden" d="M0 2h4v1H0z"/>
                <path d="M2 0h1v1H2zM3 1h1v1H3zM4 2h1v1H4zM3 3h1v1H3zM2 4h1v1H2z"/>
            </svg>
        `
    }

    renderer = Neos.Fusion:Augmenter {
        class = ${private.class}
        style = ${props.style}
        aria-label = ${props.ariaLabel}
        aria-hidden = ${props.ariaLabel ? null : props.ariaHidden}
        role = 'img'

        content = Neos.Fusion:Case {
            animatedArrow {
                condition = ${private.icon == 'animatedArrow' || private.icon == 'animatedArrowLeft'}
                renderer = ${private.animatedArrow}
            }
            neosAvatar {
                condition = ${private.icon == 'neosAvatarPrimary' || private.icon == 'neosAvatarLight' || private.icon == 'neosAvatarDark'}
                renderer = ${private.neosAvatar}
            }
            neosLogo {
                condition = ${private.icon == 'neosLogoPrimary' || private.icon == 'neosLogoLight' || private.icon == 'neosLogoDark'}
                renderer = ${private.neosLogo}
            }
            fileExists {
                condition = ${File.exists('resource://Neos.Presentation/Public/Assets/Icons/' + private.icon + '.svg')}
                renderer = ${StaticResource.content('Neos.Presentation', 'Public/Assets/Icons/' + private.icon + '.svg')}
            }
            error {
                condition = true
                renderer = ${StaticResource.content('Neos.Presentation', 'Public/Assets/Icons/notfound.svg')}
            }
        }
    }
}
