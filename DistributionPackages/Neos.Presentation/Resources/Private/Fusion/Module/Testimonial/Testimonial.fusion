prototype(Neos.Presentation:Module.Testimonial) < prototype(Neos.Fusion:Component) {

    @styleguide {
        title = "Testimonial"
        props {
            paragraph = "Lorem ipsum dolor sit amet, consetetur sadipscing elitr, sed diam nonumy eirmod tempor invidunt ut labore et dolore magna aliquyam erat, sed diam voluptua. At vero eos et accusam et justo duo dolores et ea rebum. Stet clita kasd gubergren, no sea takimata sanctus est Lorem ipsum dolor sit amet. Lorem ipsum dolor sit amet, consetetur sadipscing elitr, sed diam nonumy eirmod tempor invidunt ut labore et dolore magna aliquyam erat, sed diam voluptua. At vero eos et accusam et justo duo dolores et ea rebum. Stet clita kasd gubergren, no sea takimata sanctus est Lorem ipsum dolor sit amet."
            isQuote = true
            textLeft = false
            name = "Karsten Dambekalns"
            position = "Creative Code Engineer"
            button = "Read more"
            image.imageSource = Sitegeist.Kaleidoscope:UriImageSource {
                uri = 'https://images.unsplash.com/photo-1472099645785-5658abf4ff4e?auto=format&fit=facearea&facepad=5&ixlib=rb-4.0.3&ixid=M3wxMjA3fDB8MHxwaG90by1wYWdlfHx8fGVufDB8fHx8fA%3D%3D'
            }
        }
    }

    @propTypes {
        text = PropTypes:FromPrototype {
            prototypeName = "Neos.Presentation:Paragraph"
        }
        isQuote = PropTypes:Bool
        textLeft = PropTypes:Bool
        name = PropTypes:String
        position = PropTypes:String
        button = PropTypes:String
    }

    textLeft = null
    image = null
    isQuote = null
    paragraph = null
    button = null
    name = null
    position = null

    @private {
        textStyle = ${props.isQuote ? 'quote-style' : 'text-style'}
        textLeft = ${props.textLeft ? 'lg:flex-row-reverse flex-col-reverse' : 'lg:flex-row flex-col'}
    }

    renderer = afx`
        <Neos.Presentation:Spacing size x y>
            <div class={"flex " + private.textLeft + " justify-center items-center lg:gap-32"}>
                <Sitegeist.Kaleidoscope:Image {...props.image}
                                              attributes.class="aspect-square rounded-full lg:w-[30%] md:w-1/2 w-3/4 object-cover" />
                <div class="flex flex-col gap-y-1 mt-2 lg:w-1/2">
                    <Neos.Presentation:Icon name="quote-left" class="w-10 text-green" @if.hasQuote={props.isQuote} />
                    <Neos.Presentation:Paragraph text={props.paragraph} class={private.textStyle} />
                    <div>
                        <Neos.Presentation:Paragraph text={props.name} class=" mt-2 text-green-dark" />
                        <Neos.Presentation:Paragraph text={props.position} />
                    </div>
                    <Neos.Presentation:Button text={props.button} class="mt-6 lg:w-1/4 w-1/2" />
                </div>
            </div>
        </Neos.Presentation:Spacing>
    `
}
