prototype(Neos.Presentation:Module.Footer) < prototype(Neos.Fusion:Component) {

    @styleguide {
        title = 'Footer'
        description = 'Footer of the page'
        props {
            linkGroups = Neos.Fusion:Map {
                items = ${Array.range(1, 4)}
                itemRenderer = Neos.Fusion:DataStructure {
                    title = 'Features'
                    links = Neos.Fusion:Map {
                        items = ${Array.range(1, 5)}
                        itemRenderer = Neos.Fusion:DataStructure {
                            title = 'Just a footer link'
                            link = '#'
                        }
                    }
                }
            }

            socials = Neos.Fusion:DataStructure {
                facebook {
                    link = 'https://www.facebook.com/neoscms'
                    icon = 'facebook'
                    label = 'Facebook'
                }
                github {
                    link = 'https://github.com/neos/neos'
                    icon = 'github'
                    label = 'Github'
                }
                linkedin {
                    link = 'https://www.linkedin.com/company/neoscms'
                    icon = 'linkedin'
                    label = 'LinkedIn'
                }
                youtube {
                    link = 'https://www.youtube.com/channel/UCQp8FVUGr9gGZbU8ZgN6aaw'
                    icon = 'youtube'
                    label = 'YouTube'
                }
                slack {
                    link = 'https://slack.neos.io/'
                    icon = 'slack'
                    label = 'Slack'
                }
                mastodon {
                    link = 'https://mastodon.neos.io/'
                    icon = 'mastodon'
                    label = 'Mastodon'
                }
            }

            endlinks = Neos.Fusion:DataStructure {
                imprint {
                    link = 'https://www.neos.io/imprint.html'
                    label = 'Imprint'
                }
                privacy {
                    link = 'https://www.neos.io/privacy.html'
                    label = 'Privacy'
                }
                terms {
                    link = 'https://www.neos.io/terms.html'
                    label = 'Terms'
                }
            }
        }
    }

    @propTypes {
        linkGroups = PropTypes:Array {
            type = PropTypes:DataStructure {
                title = PropTypes:String
                links = PropTypes:Array {
                    type = PropTypes:DataStructure {
                        title = PropTypes:String
                        link = PropTypes:String
                    }
                }
            }
        }
        socials = PropTypes:Array {
            type = PropTypes:DataStructure {
                link = PropTypes:String
                icon = PropTypes:String
                label = PropTypes:String
            }
        }
        endlinks = PropTypes:Array {
            type = PropTypes:DataStructure {
                link = PropTypes:String
                label = PropTypes:String
            }
        }
    }

    renderer = afx`
        <Neos.Presentation:Background variant="gradient">
            <Neos.Presentation:Spacing y>
                <Neos.Presentation:Spacing x y="small" size>
                    <div class="flex flex-wrap gap-12 mb-10">
                        <div class="flex-grow grid gap-y-14">
                            <Neos.Presentation:Icon name="neos-white-logo" class="h-12" />
                            <Neos.Presentation:Paragraph
                                text="Neos is free and open source software licensed under GPL v3 or later. Created with passion by over 100 contributors around the world."
                                class="max-w-screen-xxs"
                            />
                        </div>

                        <div class="flex flex-grow-[3] flex-wrap gap-8">
                            <Neos.Fusion:Loop items={props.linkGroups} itemName="group">
                                <div class="flex-grow">
                                    <Neos.Presentation:Headline display="title-md" text={group.title} />
                                    <div class="grid gap-y-1 mt-6">
                                        <Neos.Fusion:Loop items={group.links} itemName="link">
                                            <a href={link.link} class="py-1 hover:underline underline-offset-8 display-body-lg">{link.title}</a>
                                        </Neos.Fusion:Loop>
                                    </div>
                                </div>
                            </Neos.Fusion:Loop>
                        </div>
                    </div>
                </Neos.Presentation:Spacing>

                <Neos.Presentation:Spacing x y="small" size>
                    <ul class="flex flex-wrap gap-4 -mx-2">
                        <Neos.Fusion:Loop items={props.socials}>
                            <li>
                                <a href={item.link} class="flex p-2 border border-transparent hover:border-current rounded-full">
                                    <Neos.Presentation:Icon name={item.icon} class="h-6 w-6" attributes.aria-label={item.label} />
                                </a>
                            </li>
                        </Neos.Fusion:Loop>
                    </ul>
                </Neos.Presentation:Spacing>

                <Neos.Presentation:Spacing x y="small" size>
                    <p class="flex flex-wrap gap-6">
                        <Neos.Fusion:Loop items={props.endlinks}>
                            <a href={item.link} class="hover:underline underline-offset-8">{item.label}</a>
                        </Neos.Fusion:Loop>
                    </p>
                </Neos.Presentation:Spacing>
            </Neos.Presentation:Spacing>
        </Neos.Presentation:Background>
    `
}
