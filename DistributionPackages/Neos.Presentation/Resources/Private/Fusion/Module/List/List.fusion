prototype(Neos.Presentation:List) < prototype(Neos.Fusion:Component) {
    @styleguide {
        title = 'Feature List of link with preview'
        description = 'List of link items with preview image'
        props {
            link = '#'
            label = 'All case studies'
            items = Neos.Fusion:Map {
                items = Neos.Fusion:DataStructure {
                    1 = ${{color: '#65a30d', label: 'E-commerce'}}
                    2 = ${{color: '#65a30d', label: 'E-commerce'}}
                    3 = ${{color: '#1e40af', label: 'Dev'}}
                    4 = ${{color: '#b91c1c', label: 'Content Platform'}}
                    5 = ${{color: '#b91c1c', label: 'Content Platform'}}
                }
                itemRenderer = Neos.Fusion:DataStructure {
                    image {
                        imageSource = Sitegeist.Kaleidoscope:DummyImageSource {
                            width = 500
                            height = 300
                            text = '5:3'
                        }
                        alternativeText = ${item.label}
                    }
                    link = '#'
                    label = 'Name of the client and his project'
                    category = ${item}
                }
            }
        }
    }

    @propTypes {
        link = PropTypes:String
        label = PropTypes:String
        items = PropTypes:DataStructure {
            image = PropTypes:DataStructure {
                imageSource = PropTypes:InstanceOf {
                    type = '\\Sitegeist\\Kaleidoscope\\Domain\\ImageSourceInterface'
                }
                alternativeText = PropTypes:String
            }
            link = PropTypes:String
            label = PropTypes:String
            category = PropTypes:DataStructure {
                label = PropTypes:String
                color = PropTypes:String
            }
        }
    }

    link = null
    label = null
    items = null

    @if.hasItems = ${Type.isArray(this.items) && Array.length(this.items)}
    renderer = afx`
        <ol class="n-counter list-preview mb-12 border-t border-t-solid border-t-gray-light divide-gray-light divide-solid divide-y text-2xl">
            <Neos.Fusion:Loop items={props.items}>
                <Neos.Presentation:List.Fragment.Item {...item} />
            </Neos.Fusion:Loop>
        </ol>
        <div @if={props.link} class="flex w-full justify-center mb-12">
            <Neos.Presentation:Link variant="link" href={props.link} text={props.label} />
        </div>
    `
}
