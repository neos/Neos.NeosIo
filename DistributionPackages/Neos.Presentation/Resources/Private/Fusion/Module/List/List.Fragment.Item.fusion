prototype(Neos.Presentation:List.Fragment.Item) < prototype(Neos.Fusion:Component) {

    @propTypes {
        image = PropTypes:DataStructure {
            imageSource = PropTypes:InstanceOf {
                type = '\\Sitegeist\\Kaleidoscope\\Domain\\ImageSourceInterface'
            }
            alternativeText = PropTypes:String
        }
        link = PropTypes:String
        label = PropTypes:String
        category = PropTypes:DataStructure {
            label = PropTypes:String
            color = PropTypes:String
        }
    }

    @private {
        hasImage = ${!!props.image.imageSource}
    }

    renderer = afx`
        <li
            x-data={private.hasImage ? "{open:false}" : null}
            x-on:mouseenter={private.hasImage ? "open=true" : null}
            x-on:mouseleave={private.hasImage ? "open=false" : null}
            class="n-counter__increment relative group flex flex-col gap-7 transition py-2 duration-500 bg-accent-yellow bg-opacity-0 min-h-32 justify-center hover:bg-opacity-100 md:flex-row md:justify-between md:items-center"
        >
            <a href={props.link} class="flex gap-4 after:absolute after:inset-0 after:z-20 md:gap-14">
                <span class="n-counter__decimal-leading-zero transition-transform group-hover:translate-x-8 delay-100 md:delay-0"></span>
                <span class="transition-transform group-hover:translate-x-8 delay-100 md:delay-0">{props.label}</span>
            </a>
            <span class="flex order-first items-baseline gap-6 md:order-none transition-transform group-hover:translate-x-8 md:basis-72 md:delay-75">
                <span class="aspect-square h-6 translate-y-1" style={"background-color:" + props.category.color}></span>
                <span>{props.category.label}</span>
            </span>
            <Neos.Presentation:Atom.AnimatedArrow class="hidden md:block group-hover:translate-x-8 delay-150 mr-16 w-5" />
            <figure
                @if={private.hasImage}
                class="absolute pointer-events-none z-40 shadow max-w-[calc(100%-100px)] w-96"
                x-cloak
                x-transition
                x-show="open"
                "x-anchor.right.offset.50"="mouse"
            >
                <Sitegeist.Kaleidoscope:Image
                    imageSource={props.image.imageSource}
                    alt={props.image.alternativeText}
                    width={384}
                    srcset="1x,2x"
                    lazy={true}
                />
            </figure>
        </li>
    `
}
