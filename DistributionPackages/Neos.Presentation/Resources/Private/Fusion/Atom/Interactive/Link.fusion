prototype(Neos.Presentation:Link) < prototype(Neos.Fusion:Component) {

    @styleguide {
        title = 'Link'
        props {
            text = 'Link Text'
            href = '#link'
        }
        useCases {
            button.props {
                text = 'Link as Button'
                href = '#link'
                variant = 'button'
            }
            icon.props {
                text = 'Link with Icon'
                href = '#link'
                icon = 'animatedArrow'
            }
        }
    }

    @propTypes {
        variant = ${PropTypes.oneOf(['button', 'link'])}
        text = ${PropTypes.string.isRequired}
        href = ${PropTypes.anyOf(PropTypes.string)}
        icon = ${PropTypes.anyOf(PropTypes.string, PropTypes.any)}
        srOnlyLabel = ${PropTypes.boolean}
    }

    text = null
    variant = 'link'
    icon = null
    srOnlyLabel = false

    attributes = Neos.Fusion:DataStructure
    class = null

    @private {
        labelText = ${props.srOnlyLabel ? '<span class="sr-only">' + props.text + '</span>' : props.text}
        icon = Neos.Fusion:Case {
            animatedArrow {
                condition = ${props.icon == 'animatedArrow'}
                renderer = Neos.Presentation:Atom.AnimatedArrow
            }
            string {
                condition = ${Type.isString(props.icon)}
                renderer = ${props.icon}
            }
            icon {
                condition = ${props.icon}
                renderer = Neos.Presentation:Icon {
                    @apply.props = ${props.icon}
                    class = "w-5"
                }
            }
        }
    }

    renderer = Neos.Fusion:Tag {
        tagName = 'a'
        attributes {
            @apply.attributes = ${props.attributes}
            class = ${Carbon.String.classNames(
                'n-link n-interactive',
                {
                    'n-interactive--button': props.variant == 'button',
                    'n-interactive--link': props.variant == 'link',
                    'group': props.icon == 'animatedArrow'
                },
                props.class
            )}
            href = ${props.href}
        }
        content = afx`
            <span>{private.labelText}</span>
            {private.icon}
        `
    }
}
