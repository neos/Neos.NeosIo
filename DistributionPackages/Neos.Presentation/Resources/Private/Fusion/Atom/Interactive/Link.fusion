prototype(Neos.Presentation:Link) < prototype(Neos.Fusion:Component) {

    @styleguide {
        title = "Link"
        props {
            text = 'Button Text'
            href = '#link'
        }
        useCases {
            button.props {
                text = 'Link as Button'
                href = '#link'
                variant = 'button'
            }
            icon {
                props.icon = afx`<svg xmlns="http://www.w3.org/2000/svg" viewBox="0 0 10 10"><path fill="none" stroke="currentColor" d="M2 6q2 0 3 1t3 1V3Q6 3 5 2T2 1v8"/></svg>`
            }
        }
    }

    @propTypes {
        variant = ${PropTypes.oneOf(['button', 'link'])}
        text = ${PropTypes.string.isRequired}
        href = ${PropTypes.anyOf(PropTypes.string)}
        icon = ${PropTypes.string}
        srOnlyLabel = ${PropTypes.boolean}
    }

    text = null
    variant = 'link'
    icon = null
    srOnlyLabel = false

    attributes = Neos.Fusion:DataStructure
    class = null

    @private {
        labelText = ${props.srOnlyLabel ? '<span class="sr-only">' + props.text + '</span>' : props.text}
    }

    renderer = Neos.Fusion:Tag {
        tagName = 'a'
        attributes {
            @apply.attributes = ${props.attributes}
            class = ${Carbon.String.classNames(
                'n-link n-interactive',
                {
                    'n-interactive--button': props.variant == 'button',
                    'n-interactive--link': props.variant == 'link'
                },
                props.class
            )}
            href = ${props.href}
        }
        content = afx`
            <span>{private.labelText}</span>
            {props.icon}
        `
    }
}
