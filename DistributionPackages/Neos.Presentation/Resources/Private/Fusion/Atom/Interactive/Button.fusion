prototype(Neos.Presentation:Button) < prototype(Neos.Fusion:Component) {
    @styleguide {
        title = 'Button'
        props {
            text = 'Button Text'
        }
        useCases {
            link.props {
                text = 'Button as Link'
                variant = 'link'
            }
            icon.props {
                text = 'Button with Icon'
                icon = 'animatedArrow'
            }
        }
    }

    @propTypes {
        variant = ${PropTypes.oneOf(['button', 'link'])}
        text = ${PropTypes.string.isRequired}
        href = ${PropTypes.anyOf(PropTypes.string)}
        icon = ${PropTypes.anyOf(PropTypes.string, PropTypes.any)}
        srOnlyLabel = ${PropTypes.boolean}
        disabled = ${PropTypes.boolean}
    }

    text = null
    variant = 'button'
    icon = null
    srOnlyLabel = false

    attributes = Neos.Fusion:DataStructure
    class = null

    @private {
        icon = Neos.Fusion:Case {
            animatedArrow {
                condition = ${props.icon == 'animatedArrow'}
                renderer = Neos.Presentation:AnimatedArrow
            }
            string {
                condition = ${Type.isString(props.icon)}
                renderer = ${props.icon}
            }
            icon {
                condition = ${props.icon}
                renderer = Neos.Presentation:Icon {
                    @apply.props = ${props.icon}
                    class = 'w-5'
                }
            }
        }
    }

    renderer = Neos.Fusion:Tag {
        tagName = 'button'
        attributes {
            @apply.attributes = ${props.attributes}
            disabled = ${props.disabled}
            class = ${Carbon.String.classNames(
                'n-button n-interactive',
                'n-interactive--' + props.variant,
                {
                    'n-interactive--disabled cursor-not-allowed': !!props.disabled,
                    'group': props.icon == 'animatedArrow'
                },
                props.class
            )}
        }
        content = afx`
            <span @if={props.text} class={props.srOnlyLabel ? "sr-only" : null}>{props.text}</span>
            {private.icon}
        `
    }
}
