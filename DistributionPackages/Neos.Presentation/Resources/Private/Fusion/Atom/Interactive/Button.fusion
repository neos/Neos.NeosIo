prototype(Neos.Presentation:Button) < prototype(Neos.Fusion:Component) {

    @styleguide {
        title = 'Button'
        props {
            text = 'Button Text'
        }
        useCases {
            link.props {
                text = 'Button as Link'
                variant = 'link'
            }
            icon.props {
                text = 'Button with Icon'
                icon = 'animatedArrow'
            }
        }
    }

    @propTypes {
        variant = ${PropTypes.oneOf(['button', 'link'])}
        text = ${PropTypes.string.isRequired}
        href = ${PropTypes.anyOf(PropTypes.string)}
        icon = ${PropTypes.anyOf(PropTypes.string, PropTypes.any)}
        srOnlyLabel = ${PropTypes.boolean}
    }

    text = null
    variant = 'button'
    icon = null
    srOnlyLabel = false

    attributes = Neos.Fusion:DataStructure
    class = null

    @private {
        labelText = ${props.srOnlyLabel ? '<span class="sr-only">' + props.text + '</span>' : props.text}
        icon = Neos.Fusion:Case {
            animatedArrow {
                condition = ${props.icon == 'animatedArrow'}
                renderer = Neos.Presentation:Atom.AnimatedArrow
            }
            string {
                condition = ${Type.isString(props.icon)}
                renderer = ${props.icon}
            }
            icon {
                condition = ${props.icon}
                renderer = Neos.Presentation:Icon {
                    @apply.props = ${props.icon}
                    class = "w-5"
                }
            }
        }
    }

    renderer = Neos.Fusion:Tag {
        tagName = 'button'
        attributes {
            @apply.attributes = ${props.attributes}
            class = ${Carbon.String.classNames(
                'n-button n-interactive',
                {
                    'n-interactive--button': props.variant == 'button',
                    'n-interactive--link': props.variant == 'link',
                    'group': props.icon == 'animatedArrow'
                },
                props.class
            )}
        }
        content = afx`
            <span>{private.labelText}</span>
            {private.icon}
        `
    }
}
