prototype(Neos.Presentation:Paragraph) < prototype(Neos.Fusion:Component) {
    @styleguide {
        title = 'Paragraph'
        props {
            text = 'Lorem ipsum dolor <em>sit amet</em>, consetetur sadipscing elitr, sed diam nonumy eirmod tempor invidunt ut labore et dolore magna aliquyam erat, sed diam voluptua. At vero eos et accusam et justo duo dolores et ea rebum. Stet clita kasd gubergren, no sea takimata sanctus est <strong>Lorem ipsum</strong> dolor sit amet. Lorem ipsum dolor sit amet, consetetur sadipscing elitr, sed diam nonumy eirmod tempor invidunt ut labore et dolore magna aliquyam erat, sed diam voluptua. At vero eos et accusam et justo duo dolores et ea rebum. Stet clita kasd gubergren, no sea takimata sanctus est Lorem ipsum dolor sit amet.'
        }
    }

    @propTypes {
        text = ${PropTypes.string}
        tagName = ${PropTypes.oneOf(['p'])}
        display = ${PropTypes.oneOf([
            'tagname',
            'headline-xl',
            'headline-lg',
            'headline-md',
            'headline-sm',
            'title-lg',
            'title-md',
            'title-sm',
            'lead',
            'body-lg',
            'body-md',
            'body-sm',
            'label-lg',
            'label-md',
            'label-sm',
            'cta-xl',
            'cta-lg',
            'cta-sm'
        ])}
    }

    text = ''
    tagName = 'p'

    // By default we use the defined tagname
    display = 'tagname'

    class = ''

    additionalClass = 'n-paragraph'
    @private.display = ${!Type.isString(props.display) && !Type.isInteger(props.display) ? 'tagname' : props.display}

    renderer = Neos.Fusion:Tag {
        tagName = ${props.tagName}
        attributes = Neos.Fusion:DataStructure {
            @apply.attributes = ${props.attributes}
            class = ${Carbon.String.classNames(
                'display-' + private.display,
                props.additionalClass,
                props.class
            )}
            style = ${props.style}
        }

        content = ${props.content || props.text}
    }
}
