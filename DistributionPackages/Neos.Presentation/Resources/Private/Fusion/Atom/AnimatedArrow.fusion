prototype(Neos.Presentation:Atom.AnimatedArrow) < prototype(Neos.Fusion:Component) {
    @styleguide {
        title = 'Animated Arrow'
        props {
            style = 'width: 90px;margin: 100px auto'
            class = 'group'
        }
    }

    @propTypes {
        style = PropTypes:String
        class = PropTypes:Union {
            string = PropTypes:String
            array = PropTypes:Array {
                type = PropTypes:String
            }
        }
        lineClass = PropTypes:Union {
            string = PropTypes:String
            array = PropTypes:Array {
                type = PropTypes:String
            }
        }
    }

    class = 'w-4'
    lineClass = 'transition-transform ease-[steps(5)] scale-x-0 group-hover:scale-x-100 duration-500 origin-right'

    renderer = afx`
        <svg
            xmlns="http://www.w3.org/2000/svg"
            viewBox="0 0 5 5"
            fill="currentColor"
            class={props.class}
            style={props.style}
        >
            <path class={props.lineClass} d="M0 2h4v1H0z"/>
            <path d="M2 0h1v1H2zM3 1h1v1H3zM4 2h1v1H4zM3 3h1v1H3zM2 4h1v1H2z"/>
        </svg>
    `
}
