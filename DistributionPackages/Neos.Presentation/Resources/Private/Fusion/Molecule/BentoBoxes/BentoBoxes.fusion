prototype(Neos.Presentation:BentoBoxes) < prototype(Neos.Fusion:Component) {
    @styleguide {
        title = 'Bento Boxes'
        props {
            headline = 'Tools that transform your digital journey'
            text = 'Take control of HTML, CSS, and JavaScript in a visual canvas. Neos generates clean, semantic code thatâ€™s ready.'
            boxes = Neos.Fusion:DataStructure {
                1 = Neos.Fusion:DataStructure {
                    headline = 'Enterprise Features like Role Management and Media Center'
                    background = 'brand'
                    image.imageSource = Sitegeist.Kaleidoscope:DummyImageSource {
                        height = 480
                        width = 360
                    }
                    assetAlign = 'center'
                    link = '#'
                    linkLabel = 'Learn more'
                }
                2 = Neos.Fusion:DataStructure {
                    headline = 'What you see, is what you get. Our True Inline Editor.'
                    background = 'blue'
                    image.imageSource = Sitegeist.Kaleidoscope:DummyImageSource {
                        width = 712
                        height = 400
                    }
                    assetAlign = 'right'
                }
                3 = Neos.Fusion:DataStructure {
                    headline = 'Multi-language and Multi-site Functionality'
                    link = '#'
                    linkLabel = 'Learn more'
                    background = 'blue-light'
                    image.imageSource = Sitegeist.Kaleidoscope:DummyImageSource {
                        width = 712
                        height = 240
                    }
                    assetAlign = 'right'
                }
                4 = Neos.Fusion:DataStructure {
                    headline = 'SEO Optimization Tools for the best performance'
                    background = 'brand'
                    image.imageSource = Sitegeist.Kaleidoscope:DummyImageSource {
                        width = 600
                        height = 500
                    }
                    assetAlign = 'left'
                }
            }
        }
    }

    @propTypes {
        headlineTagName = ${PropTypes.oneOf(['h1', 'h2', 'h3', 'h4', 'h5', 'h6'])}
        headline = PropTypes:String
        text = PropTypes:String
        boxes = PropTypes:Array {
            type = PropTypes:FromPrototype {
                prototypeName = 'Neos.Presentation:BentoBoxes.Box'
            }
        }
    }

    headlineTagName = 'h2'
    headline = null
    text = null
    boxes = ${[]}

    @private {
        boxes = ${Carbon.Array.chunk(props.boxes, 2)}
        boxHeadlineTagName = ${Carbon.String.heading(props.headlineTagName, 1)}
    }

    renderer = afx`
        <Neos.Presentation:Spacing size x y>
            <Neos.Presentation:Headline
                @if={props.headline}
                tagName={props.headlineTagName}
                content={props.headline}
                display="headline-lg"
                class="max-w-4xl"
            />
            <Neos.Presentation:Paragraph
                @if={props.text}
                content={props.text}
                display="lead"
                class="text-black/75 mt-2 max-w-xl lg:ml-auto lg:max-w-1/3"
            />
            <div class="flex flex-col gap-6 mt-16 lg:mt-0">
                <Neos.Fusion:Loop items={private.boxes} itemName="chunk" iterationName="chunkIterator">
                    <div class="flex flex-col lg:flex-row gap-6 items-stretch">
                        <Neos.Fusion:Loop items={chunk} itemName="box">
                            <Neos.Presentation:BentoBoxes.Box
                                {...box}
                                headlineTagName={private.boxHeadlineTagName}
                                alignEnd={chunkIterator.isFirst && iterator.isLast}
                            />
                        </Neos.Fusion:Loop>
                    </div>
                </Neos.Fusion:Loop>
            </div>
        </Neos.Presentation:Spacing>
    `
}
