prototype(Neos.Presentation:StepByStepHighlight.Step) < prototype(Neos.Fusion:Component) {

    @propTypes {
        inBackend = PropTypes:Bool
        index = PropTypes:Int
        heading = PropTypes:String
        text = PropTypes:String
        image = PropTypes:FromPrototype {
            prototypeName = Sitegeist.Kaleidoscope:Image
        }
    }

    inBackend = false
    index = 0

    renderer = afx`
        <div
            class="max-w-full px-4 py-1 broder-l border-l-2 flex flex-col"
            :class={"currentStep == " + props.index + "?'border-l-lightblue':'border-l-gray-light'"}
        >
            <Neos.Fusion:Augmenter
                x-on:click={'currentStep = ' + props.index}
                :class={"currentStep == " + props.index + "?'text-lightblue':'text-darkblue'"}>
                <Neos.Presentation:Headline
                    @if={props.inBackend || props.heading}
                    text={props.heading} tagName="h3" display="title-md"
                    class="text-balance cursor-pointer duration-300 mb-2"
                />
            </Neos.Fusion:Augmenter>

            <div x-show={'currentStep == ' + props.index} x-collapse :aria-hidden={'currentStep != ' + props.index}>
                <Neos.Presentation:Paragraph class="text-pretty mb-2">
                    {props.text}
                </Neos.Presentation:Paragraph>

                <figure @if={step.image && step.image.imageSource} class="my-8 md:hidden">
                    <Sitegeist.Kaleidoscope:Image {...step.image} attributes.class="mx-auto" />
                </figure>
            </div>
        </div>
    `
}
