prototype(Neos.Presentation:Background) < prototype(Neos.Fusion:Component) {

    @styleguide {
        title = "Background"
        props {
            variant = null
            content = 'Lorem ipsum dolor sit amet, consectetur adipisicing elit. Assumenda atque cumque, dignissimos ea eos error in iure laboriosam libero mollitia nam nemo omnis, perferendis quisquam recusandae reiciendis repudiandae unde ut!'
        }
        propSets {
            gradient {
                variant = 'gradient'
                content = 'Lorem ipsum dolor sit amet, consectetur adipisicing elit. Assumenda atque cumque, dignissimos ea eos error in iure laboriosam libero mollitia nam nemo omnis, perferendis quisquam recusandae reiciendis repudiandae unde ut!'
            }
        }
    }

    @propTypes {
        variant = ${PropTypes.oneOf(['gradient'])}

        tagName = ${PropTypes.string}
        style = ${PropTypes.any}
        class = ${PropTypes.any}
    }

    variant = null

    tagName = 'div'
    style = null
    class = null

    content = null

    renderer = afx`
        <Neos.Fusion:Tag
            tagName={props.tagName}
            attributes.style={props.style}
            attributes.class={AtomicFusion.classNames(
                'n-background',
                {
                    'n-background--gradient': props.variant == 'gradient',
                    'n-background--brand': props.variant == 'brand',
                    'n-background--accent-purple': props.variant == 'accent-purple',
                    'n-background--accent-blue': props.variant == 'accent-blue',
                    'n-background--accent-lightblue': props.variant == 'accent-lightblue',
                    'n-background--accent-green': props.variant == 'accent-green',
                    'n-background--accent-yellow': props.variant == 'accent-yellow',
                    'n-background--accent-orange': props.variant == 'accent-orange',
                    'n-background--accent-red': props.variant == 'accent-red'
                },
                props.class
            )}
        >
            {props.content}
        </Neos.Fusion:Tag>
    `
}
