prototype(Neos.Presentation:Background) < prototype(Neos.Fusion:Component) {
    @styleguide {
        title = 'Background'
        props {
            variant = 'gradient'
            content = 'Lorem ipsum dolor sit amet, consectetur adipisicing elit. Assumenda atque cumque, dignissimos ea eos error in iure laboriosam libero mollitia nam nemo omnis, perferendis quisquam recusandae reiciendis repudiandae unde ut!'
        }
    }

    @propTypes {
        variant = ${PropTypes.oneOf(['gradient', 'brand', 'blue-dark', 'blue', 'blue-light', 'purple', 'green', 'yellow', 'orange', 'red'])}
        tagName = ${PropTypes.string}
        style = ${PropTypes.any}
        class = ${PropTypes.any}
    }

    variant = null

    tagName = 'div'
    style = null
    class = null

    content = null

    renderer = afx`
        <Neos.Fusion:Tag
            tagName={props.tagName}
            attributes.style={props.style}
            attributes.class={Carbon.String.classNames(
                'flow-root',
                'n-background',
                props.variant && 'n-background--' + props.variant,
                props.variant == 'gradient' && 'bg-gradient-to-r from-blue-dark to-blue',
                props.class
            )}
        >
            {props.content}
        </Neos.Fusion:Tag>
    `
}
