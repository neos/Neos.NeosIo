prototype(Neos.Presentation:HighlightBoxes) < prototype(Neos.Fusion:Component) {

    @styleguide {
        title = 'Highlight Boxes'
        props {
            cols = 3
            heading = 'Tools that transform your digital journey'
            image.imageSource = Sitegeist.Kaleidoscope:DummyImageSource {
                baseWidth = 1920
                baseHeight = 1080
            }
            boxes = Neos.Fusion:DataStructure {
                1 = Neos.Fusion:DataStructure {
                    image.imageSource = Sitegeist.Kaleidoscope:DummyImageSource
                    icon = 'heart'
                    tag = 'Enterprise'
                    heading = 'Enterprise Features like Role Management and Media Center'
                    description = 'Manage your team and media assets with ease. Our role management system allows you to define roles and permissions for your team members.'
                    link = '#'
                    linkLabel = 'Learn more about roles and permissions'
                    linkTargetBlank = true
                }
                2 = Neos.Fusion:DataStructure {
                    image.imageSource = Sitegeist.Kaleidoscope:DummyImageSource
                    icon = 'email'
                    tag = 'Editor'
                    heading = 'What you see, is what you get. Our True Inline Editor.'
                    description = 'Our True Inline Editor allows you to edit your content directly on the page. No more guessing how your content will look like.'
                }
                3 = Neos.Fusion:DataStructure {
                    image.imageSource = Sitegeist.Kaleidoscope:DummyImageSource
                    icon = 'circle'
                    heading = 'Customize your content with our powerful NodeTypes.'
                    link = '#'
                    linkLabel = 'Learn more about content elements'
                }
                4 = Neos.Fusion:DataStructure {
                    image.imageSource = Sitegeist.Kaleidoscope:DummyImageSource
                    icon = 'clock'
                    heading = 'Track your success with our Analytics Dashboard.'
                    description = 'Our Analytics Dashboard gives you insights into your content performance. Track your success and optimize your content.'
                    link = '#'
                    linkLabel = 'Learn more about analytics'
                }
            }
        }
    }

    @propTypes {
        inBackend = PropTypes:Bool
        cols = ${PropTypes.oneOf([2, 3, 4])}
        displayType = ${PropTypes.oneOf(['icon', 'imageCircle', 'imageHeader'])}
        heading = PropTypes:String
        hasBackgroundImage = PropTypes:Bool
        image = PropTypes:DataStructure {
            imageSource = PropTypes:InstanceOf {
                type = '\\Sitegeist\\Kaleidoscope\\Domain\\ImageSourceInterface'
            }
        }
        boxes = PropTypes:Array {
            type = PropTypes:FromPrototype {
                prototypeName = 'Neos.Presentation:HighlightBoxes.Box'
            }
        }
    }

    inBackend = false
    cols = 2
    displayType = 'icon'
    hasBackgroundImage = false

    @private {
        backgroundImage = ${props.hasBackgroundImage && props.image && props.image.imageSource ? 'background-image:url(' + props.image.imageSource + ');' : null}
        colClass = Neos.Fusion:Match {
            @subject = ${props.cols}
            @default = ''
            3 = 'md:grid-cols-3'
            4 = 'lg:grid-cols-4'
        }
        imageWidth = Neos.Fusion:Case {
            noImage {
                condition = ${props.displayType == 'icon'}
                renderer = null
            }
            isImageCircle {
                condition = ${props.displayType == 'imageCircle'}
                renderer = 128
            }
            cols2 {
                condition = ${props.cols == 2}
                renderer = 576
            }
            cols3 {
                condition = ${props.cols == 3}
                renderer = 373
            }
            default {
                condition = true
                renderer = 272
            }
        }
        content = afx`
                <Neos.Presentation:Headline
                    @if={props.inBackend || props.heading} text={props.heading}
                    display="headline-lg" class="text-darkblue text-balance mb-16"
                />
                <div class={Carbon.String.classNames('grid gap-8 sm:grid-cols-2', private.colClass)}>
                    <Neos.Fusion:Loop items={props.boxes} itemName="box">
                        <Neos.Presentation:HighlightBoxes.Box {...box} displayType={props.displayType} imageWidth={private.imageWidth} />
                    </Neos.Fusion:Loop>
                </div>
        `
    }

    renderer = afx`
        <Neos.Presentation:Spacing
            @if={private.backgroundImage}
            size={false}
            y class="highlight-boxes bg-cover py-8"
            attributes.style={private.backgroundImage}
        >
            <Neos.Presentation:Spacing size x content={private.content} />
        </Neos.Presentation:Spacing>
        <Neos.Presentation:Spacing
            @if={!private.backgroundImage}
            size x y class="highlight-boxes"
            content={private.content}
        />
    `
}
