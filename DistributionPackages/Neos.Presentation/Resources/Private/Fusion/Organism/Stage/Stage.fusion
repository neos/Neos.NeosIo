prototype(Neos.Presentation:Stage) < prototype(Neos.Fusion:Component) {
    @styleguide {
        title = 'Stage'
        description = 'Main Stage component for header'

        props {
            headline.text = 'The First Visual Enterprise <strong>Content Management System</strong>'
            subline.text = 'Neos CMS is an open source modern digital content creation and publishing system. Unlock your potential with a system your devs and publishers will love.'
            image = '<img src="https://images.unsplash.com/photo-1496181133206-80ce9b88a853?q=80&w=3542&auto=format&fit=crop&ixlib=rb-4.0.3&ixid=M3wxMjA3fDB8MHxwaG90by1wYWdlfHx8fGVufDB8fHx8fA%3D%3D" />'
            icons = Neos.Fusion:DataStructure {
                0 {
                    text = 'Forever Open Source'
                    iconName = 'pixel-heart'
                    color = 'lightblue'
                }
                1 {
                    text = 'Planned Extensibility'
                    iconName = 'pixel-circle'
                    color = 'purple'
                }
                2 {
                    text = 'One click publishing'
                    iconName = 'pixel-clock'
                    color = 'yellow'
                }
            }
        }
    }

    @propTypes {
        navSlot = PropTypes:String
        headline = PropTypes:FromPrototype {
            prototypeName = "Neos.Presentation:Headline"
        }
        subline = PropTypes:FromPrototype {
            prototypeName = "Neos.Presentation:Paragraph"
        }
        image = PropTypes:String
        icons = PropTypes:Array {
            type = PropTypes:FromPrototype {
                prototypeName = "Neos.Presentation:SquareIcon"
            }
        }
    }

    renderer = afx`
        <section class="n-stage">
            {props.navSlot}

            <div class="n-stage-icons pointer-events-none" @if={props.icons}>
                <Neos.Fusion:Loop items={props.icons}>
                    <Neos.Presentation:SquareIcon {...item} />
                </Neos.Fusion:Loop>
            </div>

            <Neos.Presentation:Spacing x y class="flex flex-col items-center text-center max-w-screen-xl w-full relative">
                <Neos.Presentation:Headline {...props.headline} class="max-w-3xl pb-12 !text-white" tagName="h1" display="headline-lg" />
                <Neos.Presentation:Paragraph {...props.subline} class="max-w-lg pb-12" />
            </Neos.Presentation:Spacing>
        </section>
        <div class="n-stage-image">{props.image}</div>
    `
}
