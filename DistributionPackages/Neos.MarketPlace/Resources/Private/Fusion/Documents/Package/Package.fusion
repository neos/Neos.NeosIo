prototype(Neos.MarketPlace:Package) < prototype(Neos.MarketPlace:DefaultPage) {

    prototype(Neos.Seo:MetaDescriptionTag) {
        attributes {
            content = ${String.stripTags(q(node).property('description'))}
            content.@process.crop = ${String.cropAtWord(value, 160, 'â€¦')}
        }
        @if.isNotBlank = ${!String.isBlank(q(node).property('description'))}
    }

    body {
        template {
            templatePath = 'resource://Neos.MarketPlace/Private/Templates/Documents/Package.html'

            content {
                main = Neos.Fusion:Component {
                    renderer = afx`
                        <div class="stage stage--bright" id="mainContent">
                            <div class="stage__contents">
                                <main role="main" class="g">
                                    <header>
                                       <Neos.MarketPlace:Search showResult={false} />
                                    </header>
                                    <section class="gi u-w1/1 u-wm8/12 u-wl9/12">
                                        <Neos.MarketPlace:PackageRenderer/>
                                    </section>
                                    <aside class="gi u-w1/1 u-wm4/12 u-wl3/12">
                                        <Neos.MarketPlace:Package.SubMenu/>
                                    </aside>
                                </main>
                            </div>
                        </div>
                    `
                }
            }
        }
    }

    @cache {
        mode = 'dynamic'
        entryIdentifier {
            version = ${request.arguments.version ? false : 'latest'}
        }
        context {
            site = 'site'
            documentNode = 'documentNode'
            node = 'node'
        }
        entryTags {
            1 = ${Neos.Caching.nodeTag(node)}
            2 = ${Neos.Caching.descendantOfTag(node)}
        }
    }
}
