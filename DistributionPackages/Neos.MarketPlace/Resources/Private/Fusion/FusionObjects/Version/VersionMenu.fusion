prototype(Neos.MarketPlace:VersionMenu) < prototype(Neos.Fusion:Component) {
    title = ${I18n.translate('versions.releasedVersions', 'Released versions', [], 'Main', 'Neos.MarketPlace')}
    versions = Neos.MarketPlace:VersionsQuery

    renderer = afx`
        <nav class="version-nav__container">
            <header>
                <h4>{props.title}</h4>
            </header>
            <ul class="version-nav">
                <Neos.Fusion:Loop items={Array.slice(props.versions, 0, 5)} itemName="node">
                    <Neos.MarketPlace:VersionMenuItem />
                </Neos.Fusion:Loop>
            </ul>
            <details>
                <summary>
                    <strong>Toggle older versions</strong>
                </summary>
                <ul class="version-nav">
                    <Neos.Fusion:Loop items={Array.slice(props.versions, 5)} itemName="node">
                        <Neos.MarketPlace:VersionMenuItem />
                    </Neos.Fusion:Loop>
                </ul>
            </details>
        </nav>
    `
}
